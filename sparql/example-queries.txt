-------- Query 1: Discover the 10 most involved oncogenes in cancer diseases --------

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
SELECT ?gene (COUNT(?fact) AS ?numFacts)
WHERE
{
    ?fact core:expressedBy ?gene;
          core:hasType "ONCOGENE"^^xsd:string.
}
GROUP BY ?gene
ORDER BY DESC(?numFacts)
LIMIT 10

----------------------------------------------------------------


-------- Query 2: Discover the 10 most involved biomarkers in cancer diseases --------

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
SELECT ?gene (COUNT(?fact) AS ?numFacts)
WHERE
{
    ?fact core:expressedBy ?gene;
          core:hasType "BIOMARKER"^^xsd:string.
}
GROUP BY ?gene
ORDER BY DESC(?numFacts)
LIMIT 10


-------- Query 3: Discover the 10 most involved tumor suppressor genes in cancer diseases --------

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
SELECT ?gene (COUNT(?fact) AS ?numFacts)
WHERE
{
    ?fact core:expressedBy ?gene;
          core:hasType "TSG"^^xsd:string.
}
GROUP BY ?gene
ORDER BY DESC(?numFacts)
LIMIT 10


----------------------------------------------------------------


-------- Query 4: Discover the 10 most discussed genes in literature --------

PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
SELECT ?gene (COUNT(DISTINCT ?article) AS ?numArticles)
WHERE 
{
    ?fact core:expressedBy ?gene;
          core:supportedBy ?evidence.
    
    ?evidence core:extractedFrom ?article.
}
GROUP BY ?gene
ORDER BY DESC(?numArticles)
LIMIT 10


----------------------------------------------------------------


-------- Query 5: Discover the 10 most discussed cancer diseases in literature --------

PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
PREFIX umls: <http://linkedlifedata.com/resource/umls/id/>

SELECT ?disease (COUNT(DISTINCT ?article) AS ?numArticles)
WHERE 
{
    ?fact core:involves ?disease;
          core:supportedBy ?evidence.
    
    ?evidence core:extractedFrom ?article.
    FILTER (?disease NOT IN (umls:C0086692, umls:C0027651, umls:C0006826, umls:C1306459))
}
GROUP BY ?disease
ORDER BY DESC(?numArticles)
LIMIT 10


----------------------------------------------------------------


-------- Query 6: Discover the 10 most discussed facts in literature --------

PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
PREFIX umls: <http://linkedlifedata.com/resource/umls/id/>

SELECT ?disease (COUNT(DISTINCT ?article) AS ?numArticles)
WHERE 
{
    ?fact core:involves ?disease;
          core:supportedBy ?evidence.
    
    ?evidence core:extractedFrom ?article.
    FILTER (?disease NOT IN (umls:C0086692, umls:C0027651, umls:C0006826, umls:C1306459))
}
GROUP BY ?disease
ORDER BY DESC(?numArticles)
LIMIT 10


----------------------------------------------------------------


-------- Query 7: Discover the longest-discussed fact in literature --------

PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
SELECT ?gene ?relation ?disease ?pubDate (COUNT(DISTINCT ?article) AS ?numArticles)
WHERE
{
    ?fact core:expressedBy ?gene;
          core:hasType ?relation;
          core:involves ?disease;
          core:supportedBy ?evidence.
    
    ?evidence core:extractedFrom ?article.
    ?article core:publicationYear ?pubDate.
        
    {
        SELECT ?fact (COUNT(DISTINCT ?pubDate) AS ?numYears)
        WHERE 
        {
            ?fact core:supportedBy ?evidence.

            ?evidence core:extractedFrom ?article.
            ?article core:publicationYear ?pubDate.
        }
        GROUP BY ?fact
        ORDER BY DESC(?numYears)
        LIMIT 1 
    }
}   
GROUP BY ?gene ?relation ?disease ?pubDate
ORDER BY ASC(?pubDate)
