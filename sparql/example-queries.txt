-------- Query 1: Count number of genes --------

PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>

SELECT (COUNT(DISTINCT ?gene) AS ?geneCount) 
WHERE
{
    ?fact core:expressedBy ?gene.
}

------------------------------------------------


-------- Query 2: Count number of cancer diseases --------

PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>

SELECT (COUNT(DISTINCT ?cancer) AS ?cancerCount) 
WHERE
{
    ?fact core:involves ?cancer.
}

----------------------------------------------------------


-------- Query 3: Count number of facts --------

PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT (COUNT(DISTINCT ?fact) AS ?factCount) 
WHERE
{
    ?fact rdf:type core:GCS.
}

-------------------------------------------------


-------- Query 4: Find facts where BRAF gene acts as oncogene --------

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
PREFIX ncbi: <https://www.ncbi.nlm.nih.gov/gene/>

SELECT ?fact ?cancer
WHERE 
{
    ?fact core:expressedBy ncbi:673;
          core:involves ?cancer;
          core:hasType "ONCOGENE"^^xsd:string.
}

----------------------------------------------------------------------


-------- Query 5: Find facts where genes act as tumor suppressors for breast cancer --------

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
PREFIX umls: <http://linkedlifedata.com/resource/umls/id/>

SELECT ?fact ?gene
WHERE 
{
    ?fact core:involves umls:C0006142;
          core:expressedBy ?gene;
          core:hasType "TSG"^^xsd:string.
}

---------------------------------------------------------------------------------------------


-------- Query 6: Find evidence for the (LINC00667, oncogene, colorectal cancer) fact --------

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
PREFIX ncbi: <https://www.ncbi.nlm.nih.gov/gene/>
PREFIX umls: <http://linkedlifedata.com/resource/umls/id/>

SELECT ?article ?content ?GeneMention ?DiseaseMention ?CGE ?CCS ?GCI
WHERE 
{
    ?fact core:involves umls:C0009404;
          core:expressedBy ncbi:339290;
          core:hasType "ONCOGENE"^^xsd:string;
          core:supportedBy ?evidence.
    
    ?evidence 
          core:extractedFrom ?article;
          core:hasContent ?content;
          core:GSAMention ?GeneMention;
          core:DSAMention ?DiseaseMention;
          core:CGELabel ?CGE;
          core:CCSLabel ?CCS;
          core:GCILabel ?GCI.
}

----------------------------------------------------------------------------------------------


-------- Query 7: Discover the 10 most common oncogenes --------

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX core: <http://gda.dei.unipd.it/cecore/ontology/>
SELECT ?gene (COUNT(?fact) AS ?numFacts)
WHERE
{
    ?fact core:expressedBy ?gene;
          core:hasType "ONCOGENE"^^xsd:string.
}
GROUP BY ?gene
ORDER BY DESC(?numFacts)
LIMIT 10

----------------------------------------------------------------
